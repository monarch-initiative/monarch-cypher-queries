query: |
        MATCH path=(subject:gene)<-[geno:GENO:0000418|GENO:0000641!*0..1]-(feature)-[rel:RO:0002200|RO:0002610|RO:0002326|RO:0003302|RO:0004010!]->(object:disease)
        WITH subject, feature, rel, rel.iri as rel_iri, object, path

        MATCH (assoc:association)-[:OBAN:association_has_subject]->(feature),
              (assoc)-[:OBAN:association_has_object]->(object),
              (assoc)-[:OBAN:association_has_predicate]->(:Node{iri:rel_iri})

        WHERE ((NOT exists(assoc.confidence_score) OR assoc.confidence_score > 1) AND
              (subject)<-[:GENO:0000418|GENO:0000639!*0..1]-()<-[:BFO:0000051!*0..]-()-[:RO:0002200|RO:0003303|GENO:0000840|RO:0004011|RO:0004013|RO:0004012|RO:0004014]->(object))

        WITH COLLECT ({
            path:path,
            relation:rel,
            subject:subject,
            object:object,
            association_type:'gene_disease',
            subject_category:'gene',
            object_category:'disease',
            qualifier:'inferred through variant'
        }) as rows


        MATCH path=(subject:gene)<-[:GENO:0000418|GENO:0000641!]-(variant)<-[:BFO:0000051!*]-(genotype:genotype)-[rel:RO:0002200|RO:0002610|RO:0002326|RO:0003302|RO:0004010!]->(object:disease)
        WHERE (subject)<-[:GENO:0000418|GENO:0000639!*0..1]-()<-[:BFO:0000051!*0..]-()-[:RO:0002200|RO:0003303|GENO:0000840|RO:0004011|RO:0004013|RO:0004012|RO:0004014]->(object)

        WITH rows + COLLECT ({
            path:path,
            relation:rel,
            subject:subject,
            object:object,
            association_type:'gene_disease',
            subject_category:'gene',
            object_category:'disease',
            qualifier:'inferred through genotype'
        }) as rows

        UNWIND rows as row
        RETURN 
            row.path as path,
            row.relation as relation,
            row.subject as subject,
            row.object as object,
            row.association_type as association_type,
            row.subject_category as subject_category,
            row.object_category as object_category,
            row.qualifier as qualifier
        ORDER BY subject, object

subject_closure: "equivalentClass|sameAs"
